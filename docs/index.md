---
page_title: "Official Sentry Terraform Provider"
description: |-
  The Terraform provider for Sentry allows teams to configure and update Sentry project parameters via their command line. This provider is built and maintained by a community developer and officially sponsored by Sentry.
---

# Official Sentry Terraform Provider

The Sentry Terraform provider is an open-source project built and maintained by a community developer and officially sponsored by [Sentry](https://sentry.io).

The Sentry Terraform provider allows your team to configure and update Sentry project parameters including Projects, Alerts (Issues & Transactions), Dashboards, Teams, and Organizations - programmatically. This helps you easily provision Sentry while also giving you the option to work from an interface you are already familiar with. If you have questions, feature requests, or issues you'd like to report, please [contact Sentry](https://help.sentry.io/) or click on 'report an issue' to the right.

This provider utilizes the [Web APIs](https://docs.sentry.io/api/) to interact with Sentry resources.

## Setup

### Authentication

You will need to configure the provider by providing an authentication token. You can create an authentication token within Sentry by [creating an internal integration](https://docs.sentry.io/product/integrations/integration-platform/internal-integration/#auth-tokens). This is also available for self-hosted Sentry.

```terraform
provider "sentry" {
  token = "my-auth-token"
}
```

It's best practice not to store the authentication token in plain text. As an alternative, the provider can source the authentication token from the `SENTRY_AUTH_TOKEN` environment variable. If you choose to do this, you can omit the token variable from the configuration block above.

```terraform
provider "sentry" {}
```

### Self-hosted Sentry

If you are self-hosting Sentry, you can set the base URL here. The URL format must be in the format `https://[hostname]/api/`.

```terraform
# Configure the Sentry Provider
provider "sentry" {
  base_url = "https://example.com/api/"
}
```

## Example Usage

```terraform
# Configure the Sentry Provider for US data storage location (default)
provider "sentry" {
  token = var.sentry_auth_token

  # If you want to be explicit, you can specify the base URL for the US region.
  # base_url = "https://us.sentry.io/api/"
  # or
  # base_url = "https://sentry.io/api/"
}

# Configure the Sentry Provider for EU data storage location
provider "sentry" {
  token = var.sentry_auth_token

  base_url = "https://de.sentry.io/api/"
}

# Configure the Sentry Provider for self-hosted Sentry
provider "sentry" {
  token = var.sentry_auth_token

  # The URL format must be "https://[hostname]/api/".
  base_url = "https://example.com/api/"
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `base_url` (String) The target Sentry Base API URL follows the format `https://[hostname]/api/`. The default value is `https://sentry.io/api/`, which is an alias for `https://us.sentry.io/api/` (US data storage location). To change the data storage location to the EU, set the value to `https://de.sentry.io/api/`. This value is required for non-US storage locations or Sentry On-Premise deployments. The value can be sourced from the `SENTRY_BASE_URL` environment variable.
- `token` (String, Sensitive) The authentication token used to connect to Sentry. The value can be sourced from the `SENTRY_AUTH_TOKEN` environment variable.



## Typical Usage

Below is a full example showcasing typical usage:

```terraform
terraform {
  required_providers {
    sentry = {
      source = "jianyuan/sentry"
    }
  }
}

provider "sentry" {
  token = "xxx"
}

data "sentry_organization" "main" {
  # Taken from URL: https://sentry.io/organizations/[slug]/issues/
  slug = "my-sentry-organization"
}

output "organization" {
  value = data.sentry_organization.main
}

#
# Team
#

resource "sentry_team" "main" {
  organization = data.sentry_organization.main.id
  name         = "My team"
}

output "team" {
  value = sentry_team.main.id
}

#
# Project
#

resource "sentry_project" "main" {
  organization = sentry_team.main.organization
  team         = sentry_team.main.id
  name         = "My project"
  platform     = "python"
}

output "project" {
  value = sentry_project.main.id
}

#
# Client key
#

data "sentry_key" "main" {
  organization = sentry_project.main.organization
  project      = sentry_project.main.id

  first = true
}

output "client_key" {
  value = data.sentry_key.main
}
```
